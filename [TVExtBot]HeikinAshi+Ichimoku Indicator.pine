//@version=5
// ##############################################################하이킨아시+일목균형 활용전략##############################################################
// <매수진입>
//   현재HA고가 > 이전HA고가(과거2개봉높은수치) and 종가 > 후행스팬 and 종가 > 일목윗구름대 and (전환선 >= 기준선 or 종가 > 기준선)
// <매수청산>
//   현재HA저가 < 이전HA저가(과거2개봉낮은수치) and (전환선 < 기준선 or 종가 < 선행스팬A or 종가 < 기준선 or 종가 < 일목윗구름대 or 종가 < 후행스팬)
// <매도진입>
//   현재HA저가 < 이전HA저가(과거2개봉낮은수치) and 종가 < 후행스팬 and 종가 < 일목아래구름대 and (전환선 <= 기준선 or 종가 < 기준선)
// <매도청산>
//   현재HA고가 > 이전HA고가(과거2개봉높은수치) and (전환선 > 기준선 or 종가 > 선행스팬A or 종가 > 기준선 or 종가 > 일목아래구름대 or 종가 > 후행스팬)
//#########################################################################################################################################################
indicator('[TVExtBot]HeikinAshi+Ichimoku Indicator', shorttitle='[TVExtBot]HaI', overlay=true)

hahigh = request.security(ticker.heikinashi(syminfo.tickerid), timeframe.period, high)
halow = request.security(ticker.heikinashi(syminfo.tickerid), timeframe.period, low)

enableLong = input(title='차트에 롱적용체크', defval=true)
enableShort = input(title=' 차트에 숏적용체크', defval=true)
TenkanSenPeriods = input.int(9, minval=1, title='전환선')
KijunSenPeriods = input.int(26, minval=1, title='기준선')
SenkouSpan2Periods = input.int(52, minval=1, title='선행스팬')
displacement = input.int(26, minval=1, title='Displacement')
donchian(len) =>
    math.avg(ta.lowest(len), ta.highest(len))
TenkanSen = donchian(TenkanSenPeriods)
KijunSen = donchian(KijunSenPeriods)
SenkouSpan1 = math.avg(TenkanSen, KijunSen)
SenkouSpan2 = donchian(SenkouSpan2Periods)
SenkouSpanH = math.max(SenkouSpan1[displacement - 1], SenkouSpan2[displacement - 1])
SenkouSpanL = math.min(SenkouSpan1[displacement - 1], SenkouSpan2[displacement - 1])
ChikouSpan = close[displacement - 1]

plot(TenkanSen, color=color.new(color.blue, 0), title='전환선', linewidth=2)
plot(KijunSen, color=color.new(color.maroon, 0), title='기준선', linewidth=3)
plot(close, offset=-displacement, color=color.new(color.orange, 0), title='후행스팬', linewidth=2)
sa = plot(SenkouSpan1, offset=displacement, color=color.new(color.green, 0), title='선행스팬1', linewidth=2)
sb = plot(SenkouSpan2, offset=displacement, color=color.new(color.red, 0), title='선행스팬2', linewidth=3)
fill(sa, sb, color=SenkouSpan1 > SenkouSpan2 ? color.new(color.green, 90) : color.new(color.red, 90))

longCondition = hahigh > math.max(hahigh[1], hahigh[2]) and close > ChikouSpan and close > SenkouSpanH and (TenkanSen >= KijunSen or close > KijunSen)
longClose = halow < math.min(halow[1], halow[2]) and (TenkanSen < KijunSen or close < SenkouSpan1 or close < KijunSen or close < SenkouSpanH or close < ChikouSpan)

long_position_size = 0
if long_position_size[1] > 0
    long_position_size := long_position_size[1]
    long_position_size

isLongPositionEntry = false
isLongPositionClose = false
if longCondition and long_position_size == 0
    long_position_size := 1
    isLongPositionEntry := true
    isLongPositionEntry
if longClose and long_position_size > 0
    long_position_size := 0
    isLongPositionClose := true
    isLongPositionClose

shortCondition = halow < math.min(halow[1], halow[2]) and close < ChikouSpan and close < SenkouSpanL and (TenkanSen <= KijunSen or close < KijunSen)
shortClose = hahigh > math.max(hahigh[1], hahigh[2]) and (TenkanSen > KijunSen or close > SenkouSpan1 or close > KijunSen or close > SenkouSpanL or close > ChikouSpan)

short_position_size = 0
if short_position_size[1] > 0
    short_position_size := short_position_size[1]
    short_position_size

isShortPositionEntry = false
isShortPositionClose = false
if shortCondition and short_position_size == 0
    short_position_size := 1
    isShortPositionEntry := true
    isShortPositionEntry
if shortClose and short_position_size > 0
    short_position_size := 0
    isShortPositionClose := true
    isShortPositionClose

plotshape(enableLong and isLongPositionEntry, title='Buy', color=color.new(color.green, 0), textcolor=color.new(color.green, 0), style=shape.triangleup, location=location.belowbar, size=size.small, text='매수진입', offset=0)
plotshape(enableLong and isLongPositionClose, title='Buy Close', color=color.new(color.green, 0), textcolor=color.new(color.white, 0), style=shape.labeldown, location=location.abovebar, size=size.small, text='매수\n청산', offset=0)
plotshape(enableShort and isShortPositionEntry, title='Sell', color=color.new(color.red, 0), textcolor=color.new(color.red, 0), style=shape.triangledown, location=location.abovebar, size=size.small, text='매도진입', offset=0)
plotshape(enableShort and isShortPositionClose, title='Sell Close', color=color.new(color.red, 0), textcolor=color.new(color.white, 0), style=shape.labelup, location=location.belowbar, size=size.small, text='매도\n청산', offset=0)

alertcondition(isLongPositionEntry, title='매수진입', message='메시지설정')
alertcondition(isLongPositionClose, '매수청산', '메시지설정')
alertcondition(isShortPositionEntry, title='매도진입', message='메시지설정')
alertcondition(isShortPositionClose, '매도청산', '메시지설정')

